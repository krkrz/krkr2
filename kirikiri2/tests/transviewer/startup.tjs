// startup.tjs - �{�^�����C��
// Copyright (C)2001-2009, W.Dee  ��ρE�z�z�͎��R�ł�

class KAGLayer extends Layer // KAG �p�� ButtonLayer.tjs �����̂܂ܗ��p���邽��
{
	function KAGLayer()
	{
		super.Layer(...);
	}

	function finalize()
	{
		super.finalize(...);
	}
}

Scripts.execStorage("ButtonLayer.tjs");
Scripts.execStorage("SliderLayer.tjs");


class LSliderLayer extends SliderLayer
{
	function LSliderLayer()
	{
		super.SliderLayer(...);
	}

	function finalize()
	{
		super.finalize(...);
	}

	function onChange(pos)
	{
		// �e ( ContolLayer ) �ɂ��C�x���g��ʒm
		parent.sliderLayerChange(this, pos);
	}
}

class ControlLayer extends Layer // �R���g���[�����C���N���X
{
	var startButton;
	var timeSlider;
	var vagueSlider;
	var dragOriginX;
	var dragOriginY;
	var dragging = false; // �h���b�O�����ǂ���

	function ControlLayer()
	{
		super.Layer(...);
		setPos(32, 32);
		setImageSize(200, 85);
		setSizeToImageSize();
		fillRect(0, 0, width, height, 0xC0ffffff);
		fillRect(2, 2, width-4, 7, 0xffffffff);

		drawText(5, 15, "Time", 0, 255);
		timeSlider = new LSliderLayer(window, this);
		timeSlider.left = 75;
		timeSlider.top = 15;
		timeSlider.width = 120;
		timeSlider.height = 12;
		timeSlider.visible = true;
		timeSlider.min = 200;
		timeSlider.max = 10000;
		timeSlider.position = 1500;
		timeSlider.cursor = crArrow;

		drawText(5, 30, "Vague", 0, 255);
		vagueSlider = new LSliderLayer(window, this);
		vagueSlider.left = 75;
		vagueSlider.top = 30;
		vagueSlider.width = 120;
		vagueSlider.height = 12;
		vagueSlider.visible = true;
		vagueSlider.min = 1;
		vagueSlider.max = 512;
		vagueSlider.position = 128;
		vagueSlider.cursor = crArrow;

		startButton = new ButtonLayer(window, this);
		startButton.left = 5;
		startButton.top = 60;
		startButton.width = width - 10;
		startButton.height = 16;
		startButton.caption = '�X�^�[�g';
		startButton.visible = true;

		visible = true;
	}

	function finalize()
	{
		invalidate startButton;
		super.finalize(...);
	}

	function onMouseMove(x, y)
	{
		// �}�E�X���ړ�����
		if(dragging)
		{
			var px = parent.cursorX;
			var py = parent.cursorY;
			var l = px - dragOriginX;
			var t = py - dragOriginY;
			setPos(l, t);
		}

		if(y < 10)
			cursor = crSizeAll;
		else
		cursor = crDefault;
	}

	function onMouseDown(x, y, button)
	{
		// �}�E�X�{�^���������ꂽ
		if(y < 10)
		{
			dragging = true;
			dragOriginX = x;
			dragOriginY = y;
		}
	}

	function onMouseUp(x, y, button)
	{
		// �}�E�X�{�^���������ꂽ
		dragging = false;
	}

	function onMouseLeave()
	{
		// �}�E�X�����C���̈悩��o�Ă�����
//		dragging = false;
		super.onMouseLeave(...);
	}

	function dispFileName(fn)
	{
		// �t�@�C������\��
		if(fn != '')
			fn = Storages.extractStorageName(fn);
		else
			fn = "���[���摜���h���b�v���Ă�������";
		fillRect(5, 45, width-5, 12, 0xC0ffffff);
		drawText(5, 45, fn, 0, 255);
	}

	function sliderLayerChange(object, pos)
	{
		// �X���C�_�[�̈ʒu���ύX���ꂽ�Ƃ�
		if(object == timeSlider)
		{
			fillRect(32, 15, 50, 12, 0xc0ffffff);
			drawText(32, 15, pos + "ms",0,255);
		}
		else if(object == vagueSlider)
		{
			if(pos == 512) pos = 60000;
			fillRect(40, 30, 50, 12, 0xc0ffffff);
			drawText(40, 30, pos, 0, 255);
		}
	}
}

class MainWindow extends Window // ���C���E�B���h�E�N���X
{
	var foreLayer; // �\
	var backLayer; // ��
	var controlLayer; // �R���g���[�����C���̍쐬
	var phase = true;
	var transFile;

	function MainWindow()
	{
		// MainWindow �R���X�g���N�^
		super.Window();

		borderStyle = bsSingle;
		setInnerSize(640, 480);

		caption = System.title = "�g�����W�V�����r���[�A";

		// �v���C�}�����C��(�\)���쐬
		add(foreLayer = new Layer(this, null));
		foreLayer.loadImages('1');
		foreLayer.setSizeToImageSize();

		// �����C���̍쐬
		add(backLayer = new Layer(this, primaryLayer));
		backLayer.type = ltCoverRect;

		// �R���g���[�����C���̍쐬
		add(controlLayer = new ControlLayer(this, primaryLayer));
		controlLayer.dispFileName("");

		// �\����Ԃ�
		visible = true;
	}

	function finalize()
	{
		super.finalize(...);
	}

	function action(ev)
	{
		// �C�x���g�W��n���h��
		var type = ev.type;
		var target = ev.target;
		if(type == 'onTransitionCompleted')
		{
			// �g�����W�V�������I������
			// ���ƕ\�̊Ǘ�������ւ���
			var tmp = foreLayer;
			foreLayer = backLayer;
			backLayer = tmp;
		}
		else if(controlLayer !== void && target == controlLayer.startButton &&
			type == 'onClick')
		{
			// �u�X�^�[�g�v�{�^���������ꂽ
			beginTransition();
		}
	}

	function beginTransition()
	{
		// �g�����W�V�����̊J�n
		if(transFile != '')
		{
			foreLayer.stopTransition();
			backLayer.loadImages(phase?'2':'1'); // ���̉摜��ǂݍ���
			backLayer.setSizeToImageSize();
			phase = !phase;
			var vague = controlLayer.vagueSlider.position;
			if(vague == 512) vague = 60000;
			foreLayer.beginTransition('universal', false, backLayer,
				%[rule:transFile, time:controlLayer.timeSlider.position,
				vague:vague]);
		}
		else
		{
			System.inform("�G�N�X�v���[���Ȃǂ���u�g�����W�V�����r���[�A�v�E�B���h�E��\n"
				"���[���摜�t�@�C�����h���b�v���Ă��������B");
		}
	}

	function onFileDrop(list)
	{
		// �t�@�C�����E�B���h�E�Ƀh���b�v���ꂽ�Ƃ�
		super.onFileDrop(...);
		controlLayer.dispFileName(transFile = list[0]);
	}
}

var win = new MainWindow();
