/**
 * 環境SEオブジェクト
 */
class KAGEnvSE extends KAGEnvObject {

    var id;
	var storage;  // 参照しているファイル名
    var count; // 参照されたカウント値

	var play;
	var stop;
	var fade;
	var wait;
    var waitFade;

    /**
     * セーブ処理
     */
    function onStore(f) {
		f.storage = storage;
		f.id    = id;
		f.count = count;
		if (play !== void) {
			f.play = %[];
			(Dictionary.assign incontextof f.play)(play, false);
		}
		if (stop !== void) {
			f.stop  = stop;
		}
		if (fade !== void) {
			f.fade = %[];
			(Dictionary.assign incontextof f.fade)(fade, false);
		}
	}

	function onRestore(f) {
		if (f !== void) {
			storage = f.storage;
			id   = f.id;
			count = f.count;
			if (f.play !== void) {
				play = f.play;
			}
			if (f.stop !== void) {
				stop = f.stop;
			}
			if (f.fade !== void) {
				f.fade = %[];
			}
		}
	}
	
    /**
     * コンストラクタ
     */
    function KAGEnvSE(env, id) {
		super.KAGEnvObject(env, "se" + id);
		this.id = id;
    }

    /**
     * 再生処理
     * @param param 再生対象ファイル
     */
	function setPlay(param, elm) {
		if (param !== void) {
			stop = void;
			if (!isSkip() || elm.loop) {
				play = %[];
				(Dictionary.assign incontextof play)(elm, false);
				play.storage = storage = param;
				if (play.fade === void && play.time === void) {
					fade = %[];
					fade.volume = 100;
				}
			} else {
				play = void;
				storage = void;
			}
		}
    }

    /**
     * 停止処理
     * @param param フェードアウト時間
     */
	function setStop(param, elm) {
		play = void;
		fade = void;
		storage = void;
		var time;
		if (elm !== void && elm.time !== void) {
			time = +elm.time;
		} else {
			time = +param;
		}
		stop = time;
    }

    /**
     * 音量フェード
     * @param param フェード時間
     */
	function setFade(param, elm) {
		fade = %[];
		fade.volume = param;
		fade.time   = elm.time;
    }

    /**
     * 終了待ち
     */
    function setWait(param, elm) {
        wait = %[];
        (Dictionary.assign incontextof wait)(elm, false);
        wait.buf = id;
        if (wait.canskip === void) {
            wait.canskip = true;
        }
    }

    /**
     * フェード終了待ち
     */
    function setWaitFade(param, elm) {
		waitFade = %[];
		(Dictionary.assign incontextof waitFade)(elm, false);
		waitFade.buf = id;
		if (waitFade.canskip === void) {
			waitFade.canskip = true;
		}
    }

    var secommands = %[
	tagname : null, 
    storage : setPlay incontextof this,
    play : setPlay incontextof this,
    stop : setStop incontextof this,
    fade : setFade incontextof this,
	wait : setWait incontextof this,
    waitfade : setWaitFade incontextof this,
    loop : null,
    time : null,
    start : null,
    canskip : null,
    buf : null,
    name : null,
        ];

    var doflag;

    /**
     * コマンドの実行
     * @param cmd コマンド
     * @param param パラメータ
     * @param elm 他のコマンドも含む全パラメータ
     * @return 実行が行われた場合 true
     */
    function doCommand(cmd, param, elm) {
        var func;
        if ((func = secommands[cmd]) !== void) {
            if (func != null) {
                func(param, elm);
                doflag = true;
            }
            return true;
        }
        // 再生コマンドとみなす
		setPlay(cmd, elm);
        doflag = true;
        return true;
    }

    /**
     * KAG タグ処理
     * @param elm 引数
     */
    function tagfunc(elm) {
		// dm("SE 用ファンクション呼び出し!");
		wait = void;
        waitFade = void;
		doflag = false;
		foreach(elm, doCommand);
        if (!doflag && elm.tagname != "se") {
			setPlay(elm.tagname, elm);
        }
		sync();
		if (waitFade !== void) {
			return env.waitSEFade(waitFade);
		} else if (wait !== void) {
			return env.waitSEStop(wait);
		}
		return 0;
    }

	function sync() {
		if (!isJump()) {
			if (fade !== void) {
				env.setSEFade(id, fade);
				fade = void;
			}
			if (play !== void) {
				if (!isSkip() || play.loop || !env.nosewhenskip) {
					env.playSE(id, isSkip() ? 0 : +play.time, play);
				}
				play = void;
			}
		}
		if (stop !== void) {
			env.stopSE(id, isSkip() ? 0 : stop);
			stop = void;
		}
	}

};
