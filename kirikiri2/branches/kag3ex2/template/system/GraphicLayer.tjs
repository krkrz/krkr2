// GraphicLayer.tjs - グラフィック系レイヤ(背景,キャラクタ)
// Copyright (C)2001-2008, W.Dee and contributors  改変・配布は自由です

// 動画α処理用プラグイン
if (typeof global.Layer.copyBottomBlueToTopAlpha == "undefined") {
	try {
		Plugins.link("layerExBtoA.dll");
	} catch(e) {
	}
}

// 動画α処理用プラグイン
class __dummyGfxFire { function gfxFire { throw new Exception("炎エフェクトは使用できません（gfxEffectプラグインが必要です）"); } }
if (typeof global.gfxFire == "undefined") {
	try {
		Plugins.link("gfxEffect.dll");
		Debug.notice( 
			"---- gfxEffect Plugin copyright info ----\n"
			"A C-program for MT19937, with initialization improved 2002/1/26.\n"
			"Coded by Takuji Nishimura and Makoto Matsumoto.\n"
			"\n"
			"Before using, initialize the state by using init_genrand(seed)\n"
			"or init_by_array(init_key, key_length).\n"
			"\n"
			"Copyright (C) 1997 - 2002, Makoto Matsumoto and Takuji Nishimura,\n"
			"All rights reserved.\n"
			"\n"
			"Redistribution and use in source and binary forms, with or without\n"
			"modification, are permitted provided that the following conditions\n"
			"are met:\n"
			"\n"
			"1. Redistributions of source code must retain the above copyright\n"
			"notice, this list of conditions and the following disclaimer.\n"
			"\n"
			"2. Redistributions in binary form must reproduce the above copyright\n"
			"notice, this list of conditions and the following disclaimer in the\n"
			"documentation and/or other materials provided with the distribution.\n"
			"\n"
			"3. The names of its contributors may not be used to endorse or promote\n"
			"products derived from this software without specific prior written\n"
			"permission.\n"
			"\n"
			"THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS\n"
			"\"AS IS\" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT\n"
			"LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR\n"
			"A PARTICULAR PURPOSE ARE DISCLAIMED.  IN NO EVENT SHALL THE COPYRIGHT OWNER OR\n"
			"CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,\n"
			"EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,\n"
			"PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR\n"
			"PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF\n"
			"LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING\n"
			"NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS\n"
			"SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.\n"
			"\n"
			"\n"
			"Any feedback is very welcome.\n"
			"http://www.math.sci.hiroshima-u.ac.jp/~m-mat/MT/emt.html\n"
			"email: m-mat @ math.sci.hiroshima-u.ac.jp (remove space)\n"
			"------------------------------------------------------");
		invalidate global.__dummyGfxFire;
		delete     global.__dummyGfxFire;
	} catch(e) {
		//Debug.message(e.message);
		global.gfxFire = __dummyGfxFire;
	}
}

class ProvinceContext
{
	// 領域アクション用コンテキスト
	var target;
	var storage;
	var onenter;
	var onleave;
	var hint;
	var exp;
	var cursor;
	var countpage;
	var autodisable;

	function ProvinceContext() {};
    function finalize() {};
}

class GraphicLayer extends AnimationLayer
{
	// 背景/前景レイヤの基本クラス

	var comp; // 対のレイヤ
	var id; // ID
	var provinceActions = void; // クリッカブルマップの領域ごとのアクション定義
	var pointingProvince = 0; // 現在指し示している領域番号
	var loadedProvinceImage = ""; // loadImages 後に読み込まれた領域画像
	var loadedProvinceActions = ""; // loadImages 後に読み込まれた領域アクション
	var defaultCursor = crDefault; // デフォルトのカーソル
	var lastMouseDownX; // 最後にマウスがクリックされた X 座標
	var lastMouseDownY; // 最後にマウスがクリックされた Y 座標

	function GraphicLayer(win, par, name, id)
	{
		// GraphicLayer コンストラクタ
		// win    : ウィンドウオブジェクト
		// par    : 親レイヤ
		// name   : レイヤの名前
		super.AnimationLayer(win, par);

		this.name = name;
		this.id = id;
		/* 対のレイヤ は、このオブジェクトを作成後に setCompLayer で設定すること */
	}

	function finalize()
	{
		clearProvinceActions();
		super.finalize(...);
	}

	function setCompLayer(lay) { comp = lay; }

	function setDefaultCursor(c)
	{
		cursor = defaultCursor = c;
	}

	function loadImages(elm)
	{
		// elm に記述されている内容に従ってこのレイヤに画像を読み込む
		clearProvinceActions();
		loadedProvinceImage = "";
		super.loadImages(elm);
		if(elm !== void)
		{
			if(elm.mapimage !== void) super.loadProvinceImage(elm.mapimage); // 領域画像を読む
			if(elm.mapaction !== void)
			{
				internalLoadProvinceActions(elm.mapaction); // 領域アクションを読む
			}
			else
			{
				var name;
				var storage = Storages.getPlacedPath(
					name = (Storages.chopStorageExt(elm.storage) + ".ma")); // 拡張子が .ma のファイル
				if(storage != '')
					loadProvinceActions(name);
			}
		}
	}

	function loadProvinceImage(fn)
	{
		// 領域画像 fn を読み込む
		super.loadProvinceImage(fn);
		loadedProvinceImage = fn;
	}

	function assignComp()
	{
		// 対になるレイヤの内容をコピー
		assign(comp);
	}

	function assign(src)
	{
		super.assign(...);

		if(provinceActions !== void)
		{
			invalidate provinceActions;
			window.disableMouseKey();
		}
		if(src.provinceActions !== void)
		{
			(provinceActions = []).assign(src.provinceActions);
			window.enableMouseKey();
		}
		else
		{
			provinceActions = void;
		}
		loadedProvinceImage = src.loadedProvinceImage;
		loadedProvinceActions = src.loadedProvinceActions;
	}

	function beginTransition(elm)
	{
		// elm に従ってトランジションを行う
		comp.stopTransition(); // comp で行われていたトランジションも停止
		super.beginTransition(elm, comp);
	}

	function exchangeInfo()
	{
		// comp と情報を取り替える
		// すでに 画像の内容、ツリー構造は取り変わっているので
		// 名前などを取り替える
		// また、ウィンドウの管理情報も更新するようにする
		var src = comp;
		var tmp = src.name;
		src.name = name;
		name = tmp;
		tmp = src.cursor;
		src.cursor = cursor;
		cursor = tmp;
	}

	function clearImage(process = true)
	{
		// AnimationLayer.clearImage オーバーライド
		super.clearImage(process);
		loadedProvinceImage = "";
		clearProvinceActions();
	}

	function clearProvinceActions()
	{
		if(provinceActions !== void)
		{
			invalidate provinceActions;
			provinceActions = void;
			pointingProvince = 0;
			cursor = defaultCursor;
			hint = "";
			showParentHint = true;
			window.disableMouseKey();
		}
		loadedProvinceActions = "";
	}

	function internalLoadProvinceActions(fn)
	{
		// ファイル名 fn を領域アクション定義ファイルとして読み込む
		clearProvinceActions();

		var file = [];
		file.load(fn);
		var filelines = file.count;

		// ファイルに従ってスクリプトを動的に生成
		var body = "function { var a = provinceActions; \n";
		for(var i = 0; i<filelines; i++)
		{
			var line = file[i];
			if(line == "" || line[0] == ';') continue;
			var colon = line.indexOf(':');
			if(colon == -1) continue;
			body += "a[" + line.substring(0, colon) + "] = function { " +
				line.substring(colon + 1) + " ;};\n";
		}
		body += "}\n";

		provinceActions = [];
		(Scripts.eval(body) incontextof this) (); // body のコンパイルをしてこのコンテキスト上で実行

		window.enableMouseKey();
	}

	function loadProvinceActions(fn)
	{
		internalLoadProvinceActions(fn);
		loadedProvinceActions = fn;
	}

	function queryProvinceAction(n, checkzero = true)
	{
		// 領域番号 n に関するアクションを返す
		// アクションがなにも定義されていなければ void を返す
//		dm(n, provinceActions, provinceActions[n]);
		if(provinceActions === void) return void;
		if(checkzero) { if(n == 0) return void; }
		var action = provinceActions[n];
		if(action === void) return void;
		var ar = new ProvinceContext();
		(action incontextof ar) (); // ar のコンテキスト上でアクションを実行
		if(ar.target === void && ar.storage === void && ar.onenter === void &&
			ar.onleave === void && ar.hint === void && ar.exp === void &&
			ar.cursor === void && ar.countpage === void && ar.autodisable === void)
				return void;
		return ar;
	}

	function onMouseMove(x, y)
	{
		// onMouseMove イベントハンドラ
		super.onMouseMove(...);
		if(provinceActions !== void)
		{
			var n = window.messageLayerHiding ? 0 : getProvincePixel(x - imageLeft, y - imageTop);
			if(n != pointingProvince)
			{
				// 異なる領域の上を動いた場合
				if(pointingProvince != 0) onProvinceLeave(pointingProvince);
				if(n != 0) onProvinceEnter(n);
				pointingProvince = n;
			}
			if(n == 0)
			{
				hint = "";
				showParentHint = true;
				cursor = defaultCursor;
			}
		}
	}

	function onMouseLeave()
	{
		// onMouseLeave イベントハンドラ
		super.onMouseLeave(...);
		if(pointingProvince != 0)
		{
			onProvinceLeave(pointingProvince);
			pointingProvince = 0;
		}
	}

	function onProvinceEnter(n)
	{
		// 領域番号 n にマウスが移動した
		var action = queryProvinceAction(n);
		if(action !== void)
		{
			if(action.onenter !== void) Scripts.eval(action.onenter);
			if(action.hint !== void) hint = action.hint;
			if(action.cursor !== void)
				cursor = action.cursor; // カーソル指定の方法に注意(&はつけない)
			else
				cursor = window.cursorPointed;
		}
		else
		{
			hint = "";
			showParentHint = true;
			cursor = defaultCursor;
		}
	}

	function onProvinceLeave(n)
	{
		// 領域番号 n からマウスが離れた
		var action = queryProvinceAction(n);
		if(action !== void && action.onleave !== void) Scripts.eval(action.onleave);
		hint = "";
		showParentHint = true;
	}

	function processProvince(x, y)
	{
		// x, y 位置の領域で指定されたシナリオにジャンプする
		// 処理された場合は true, されなかった場合は false を返す
		var n = window.messageLayerHiding ? 0 : getProvincePixel(x - imageLeft, y - imageTop);
		var action = queryProvinceAction(n);
		if(action === void) return false;
		if(action.exp !== void) Scripts.eval(action.exp);
		if(action.storage != '' || action.target != '')
		{
			var q = queryProvinceAction(0, false);
			if(q === void || q.autodisable === void || +q.autodisable) clearProvinceActions();
			window.process(action.storage, action.target, +action.countpage);
		}
		return true;
	}

	function onMouseDown(x, y, button)
	{
		lastMouseDownX = x;
		lastMouseDownY = y;
		super.onMouseDown(...);
	}

	function store()
	{
		// 情報を辞書配列に記録
		var dic = super.store();
		dic.loadedProvinceImage = loadedProvinceImage;
		dic.loadedProvinceActions = loadedProvinceActions;
        return dic;
	}

	function restore(dic)
	{
		// dic から情報を読み出す
		super.restore(dic);
		if(dic.loadedProvinceImage !== void && dic.loadedProvinceImage !== '')
			loadProvinceImage(dic.loadedProvinceImage);
		else
			loadedProvinceImage = "";
		if(dic.loadedProvinceActions !== void && dic.loadedProvinceActions !== '')
			loadProvinceActions(dic.loadedProvinceActions);
		else
			clearProvinceActions();
    }

}

class BaseLayer extends GraphicLayer
{
	// 背景レイヤ

	function BaseLayer(win, par, name, id)
	{
		super.GraphicLayer(win, par, name, id);
		type = ltCoverRect;
		hitType = htMask;
		hitThreshold = 0;
	}

	function finalize()
	{
		super.finalize(...);
	}

	function assign(src)
	{
		// assign オーバーライド
		super.assign(src);
		// 親クラスの assign は可視・不可視などをコピーしないが、
		// 背景レイヤなのでそれでよい
	}

	function loadImages(elm)
	{
		// loadImages オーバーライド
		if(elm !== void)
		{
			elm.mode = "opaque"; // レイヤモードを変えられると困るので
			delete elm.index; // インデックスを変えられると困るので

			if(elm.visible !== void)
			{
				// visible の状態を変えられると困るので
				if(isPrimary) elm.visible = true; else elm.visible = false;
			}
		}
		super.loadImages(elm);
	}

	function restore(dic)
	{
		// restore オーバーライド
		if(isPrimary) dic.visible = true; else dic.visible = false;
		dic.absolute = 0;
		super.restore(dic);
	}

	function setOptions(elm)
	{
        // setOptions オーバーライド
		if(elm.visible !== void)
		{
			if(isPrimary) elm.visible = true; else elm.visible = false;
		}
		super.setOptions(elm);
	}

	function internalOnMouseDown(x, y, button, processprovince = true)
	{
		// 内部関数
		var provinceprocessed = false;
		if(button == mbLeft && processprovince && !window.messageLayerHiding)
			provinceprocessed = processProvince(x, y);
		if(button == mbLeft && !provinceprocessed)
		{
			// 自身がプライマリレイヤならば、ウィンドウの
			// onPrimaryClick を呼ぶ
			if(isPrimary) window.onPrimaryClick();
		}
		else if(button == mbRight)
		{
			// 自身がプライマリレイヤならば、ウィンドウの
			// onPrimaryRightClick を呼ぶ
			if(isPrimary) window.onPrimaryRightClick();
		}
	}

	function onMouseDown(x, y, button)
	{
		// マウスが押された
		super.onMouseDown(...);
		internalOnMouseDown(x, y, button);
	}

	function clearImage()
	{
		// GraphicLayer.clearImage オーバーライド
		super.clearImage(false);
		setImageSize(window.exWidth, window.exHeight);
		setSizeToImageSize();
		face = dfAlpha;
		fillRect(0, 0, imageWidth, imageHeight, 0xff000000);
	}

	function atEndOfTransition(src, withchildren, exchange)
	{
		// atEndOfTransition オーバーライド
		super.atEndOfTransition(...);
		if(src == null)
		{
			//・背景レイヤがsrcなしでトランジション(children=true)
			//　何もしなくてよいが、終了時に子レイヤは非表示になる。
			//・背景レイヤがsrcなしでトランジション(children=false)
			//　本当に何もしない。
		}
		else
		{
			if(withchildren)
			{
				//・背景レイヤがsrcありでトランジション(children=true)
				//　重要な情報をトランジション元と交換、しかも子レイヤに対してもこの
				//　「重要な情報を相手と交換」の指示を出す。exchange=false の場合はさらに
				//　トランジション元の情報をトランジション先にコピー。これも、子レイヤすべて
				//　に対しても指示する。
				if(!exchange)
				{
					assign(src);
					window.callAssignTransSrc();
				}
				super.exchangeInfo();
				window.exchangeForeBack();
				window.callExchangeInfo();
            }
			else
			{
				//・背景レイヤがsrcありでトランジション(children=false)
				//　重要な情報をトランジション元と交換。子レイヤに対しては
				//　「重要な情報を相手と交換」の指示をださない。exchange=false の場合は
				//　さらにトランジション元の情報をトランジション先にコピー。
				//　これも子レイヤに対しては指示しない。
				if(!exchange)
				{
					assign(src);
				}
				exchangeInfo();
			}
		}
	}

	function exchangeInfo()
	{
		// exchangeInfo オーバーライド
		super.exchangeInfo();
		window.swapBaseLayer();
	}

    property isPrimary {
        getter() {
            return this === window.fore.base;
        }
    }

}

// レイヤ動画再生制御用
class MovieControlLayer extends VideoOverlay {

	var targetLayer; //< 動画表示用 
	var alpha;       //< α動画か？
	var status;      //< 再生ステータス
	
	var layers = [];
	
	function MovieControlLayer(window) {
		super.VideoOverlay(window);
		targetLayer = new Layer(window, window.primaryLayer);
		
		mode = vomLayer;
		layer1 = targetLayer;
		status = "unload";
	}

	function clear() {
		var layers2 = [];
		layers2.assign(this.layers);
		for (var i=0;i<layers2.count;i++) {
			layers2[i].notifyStopMovie();
		}
	}

	function finalize() {
		stop();
		layer1 = null;
		invalidate targetLayer;
	}

	property isPlayingMovie {
		getter() {
			return status == "play" && layers.count > 0;
		}
	}

	// レイヤ登録
	function entry(layer) {
		layers.remove(layer);
		layers.add(layer);
	}

	// レイヤ解除
	function leave(layer) {
		layers.remove(layer);
	}

	/**
	 * ムービー再生開始
	 * @param filename ファイル名
	 * @param alpha α動画扱い
	 * @param loop ループ指定
	 * @param ovtype 合成モード
	 */
	function start(filename, loop=false, alpha=0) {
		this.alpha = alpha;
		targetLayer.name = "動画:" + filename;
		this.loop=loop;
		open(filename);
		play();
	}

	// 外部から停止
	function stop() {
		super.stop();
		clear();
	}
	
	// ムービー更新処理
	function onFrameUpdate() {
		//dm("更新:" + targetLayer.name);
		switch (alpha) {
		case 0:
			targetLayer.fillAlpha();
			break;
		case 1:
			targetLayer.height = targetLayer.imageHeight / 2;
			targetLayer.copyBottomBlueToTopAlpha();
			break;
		case 2:
			targetLayer.width = targetLayer.imageWidth / 2;
			targetLayer.copyRightBlueToLeftAlpha();
			break;
		}
		for (var i=0;i<layers.count;i++) {
			layers[i].assignImagesForMovie(targetLayer);
			layers[i].setSize(targetLayer.width, targetLayer.height);
		}
	}

	// ステータス変更
	function onStatusChanged(status) {
		//dm("ステート:" + targetLayer.name + ":" + status);
		if (this.status == "play" && status == "stop") {
			clear();
		}
		this.status = status;
	}
};

// レイヤの炎エフェクト処理用
class FireController extends gfxFire
{
	var	playing = false; // タイマーが始動しているかどうか
	var targetLayer; // 処理対象レイヤ
	var	seedLayer; // シード素材レイヤ
	var colorLayer; // 色指定用
	var	lastTick; // 前回処理時tick値
	var layers = []; // 描画対象
	var info = %[]; // 指定パラメータ保持用
	
	function FireController(window) {
		super.gfxFire();
		targetLayer = new Layer(window, window.primaryLayer);
		seedLayer = new Layer(window, window.primaryLayer);
		colorLayer = new Layer(window, window.primaryLayer);
		playing = false;
	}

	function finalize() {
		stop();
	}

	property isPlaying {
		getter() {
			return playing && layers.count > 0;
		}
	}

	property width {
		getter() {
			return targetLayer.width;
		}
	}

	property height {
		getter() {
			return targetLayer.height;
		}
	}
	// レイヤ登録
	function entry(layer) {
		layers.remove(layer);
		layers.add(layer);
	}

	// レイヤ解除
	function leave(layer) {
		layers.remove(layer);
	}

	// ----------------------------------

	var params = [
		"scalingCoeff",
		"textureFilterType"
		];
	
	var wrapParams = [
		"forceH",
		"forceV",
		"boundRangeH",
		"boundRangeV"
		];	
	
	var coolParams = [
		"numOfBlurForCoolMap",
		"textureFilterType",
		"coolRange",
		"coolStrength",
		"coolParticleDensityDenominator",
		"coolParticleDensityNumerator",
		"edgeSmoothing"
		];

	// パラメータ更新処理
	function updateParam(elm, params) {
		var update = false;
		for (var i=0;i<params.count;i++) {
			var name = params[i];
			var ename = name.toLowerCase();
			if (elm[ename] !== void) {
				info[ename] = elm[ename];
				this[name] = +elm[ename];
				update = true;
			}
		}
		return update;
	}

	function blend(c1,c2,r,max) {
		return (c1 * r + c2 * (max-r)) / max;
	}
	
	// カラー更新処理
	function updateColor(elm) {
		if (elm.firelayer !== void) {
			// 色レイヤ指定
			setCustomColorTable(colorLayer);
			colorLayer.loadImages(elm.firelayer);
			applyCustom();
			info.firelayer = elm.firelayer;
		} else if (elm.firecolor !== void) {
			switch (elm.firecolor) {
			case "blue":
				applyBlue();
				break;
			case "red":
				applyRed();
				break;
			case "white":
				applyWhite();
				break;
			default:
				// カラー指定で対応
				colorLayer.setSize(256,1);
				colorLayer.fillRect(0,0,256,1,0xffffffff);
				var colors = elm.firecolor.split(":");
				var n = colors.count - 1;
				var start = 0;
				var color1 = +colors[0];
				var color2;
				var start=0;
				var end=0;
				var c = 0;
				while (c<n) {
					start = (int)(255.0 * c / n);
					color2 = +colors[c];
					do {
						c++;
					} while (c<n && colors[c] == "");
					end   = (int)(255.0 * c / n);
					color1 = +colors[c];
					var diff  = end - start;
					//dm("start:%d %d(%06x)-%d(%06x)".sprintf(diff,start,color2,end,color1));
					var r1 = (color1 >> 16) & 0xff;
					var g1 = (color1 >> 8) & 0xff;
					var b1 = (color1      ) & 0xff;
					var r2 = (color2 >> 16) & 0xff;
					var g2 = (color2 >> 8) & 0xff;
					var b2 = (color2      ) & 0xff;
					for (var i=0;i<diff;i++) {
						var color = ((blend(r1,r2,i,diff) << 16) |
									 (blend(g1,g2,i,diff) << 8) |
									 (blend(b1,b2,i,diff)));
						//dm("color:%d:%08x".sprintf(start+i,color));
						colorLayer.setMainPixel(start+i,0,color);
					}
				}
				for (var i=end;i<256;i++) {
					//dm("Xcolor:%d:%08x".sprintf(i,color1));
					colorLayer.setMainPixel(i,0,color1);
				}
				var gamma = elm.firegamma !== void ? +elm.firegamma : 1;
				for (var i=0;i<256;i++) {
					var gamma = Math.pow(i / 255.0, 1.0/gamma) * 255;
					colorLayer.setMaskPixel(i,0,gamma);
					//dm("trans:%d:%d".sprintf(i,gamma));
				}
				setCustomColorTable(colorLayer);
				applyCustom();
			}
			info.firegamma = elm.firegamma;
			info.firecolor = elm.firecolor;
		}
	}

	/**
	 * エフェクト再生開始
	 * @param storage シード画像ファイル名
	 * @param elm 追加パラメータ
	 */
	function start(storage, elm) {
		if (!playing) {

			info.storage = storage;
			info.firemargin = elm.firemargin;
			info.firewidth  = elm.firewidth;
			info.fireheight = elm.fireheight;
			info.fireleft   = elm.fireleft;
			info.firetop    = elm.firetop;

			if (typeof storage == "Object" && storage instanceof "Layer") {
				seedLayer.assignImages(storage);
			} else {
				seedLayer.loadImages(storage);
			}
			seedLayer.setSizeToImageSize();
			var width  = elm.firewidth  !== void ? +elm.firewidth  : seedLayer.width;
			var height = elm.fireheight !== void ? +elm.fireheight : seedLayer.height;
			var left   = elm.fireleft !== void ? +elm.fireleft : 0;
			var top    = elm.firetop  !== void ? +elm.firetop  : 0;
			if (elm.firemargin !== void) {
				var firemargin = +elm.firemargin;
				width  += firemargin * 2;
				height += firemargin * 2;
				left += firemargin;
				top  += firemargin;
			}

			width = width + 15 \ 16;
			height = height + 15 \ 16;
			targetLayer.setSize(width,height);
			targetLayer.fillRect(0,0,width,height,0);
//			targetLayer.face = dfAddAlpha;
//			targetLayer.type = ltAddAlpha;
			
			// パラメータ更新
			updateParam(elm, params);
			updateParam(elm, wrapParams);
			updateParam(elm, coolParams);
			updateColor(elm);
			
			init(targetLayer);
			setFireSeed(seedLayer);
			setFireSeedPos(left, top);
			
			lastTick = System.getTickCount();
			playing = true;
		}
	}

	// パラメータ更新
	function setOptions(elm) {
		if (playing) {
			updateParam(elm, params);
			if (updateParam(elm, wrapParams)) {
				updateWarpMap();
			}
			if (updateParam(elm, coolParams)) {
				updateCoolMap();
			}
			udpateColor(elm);
		}
	}

	// 停止
	function stop() {
		if (playing) {
			clearFireSeed();
			var layers2 = [];
			layers2.assign(this.layers);
			for (var i=0;i<layers2.count;i++) {
				layers2[i].notifyStopFire();
			}
			playing = false;
		}
	}

	// 更新処理用
	function update(tick) {
		if (playing) {
			var	interval = tick - lastTick;
			if (interval > 16 ) {
				cycle();
				for (var i=0;i<layers.count;i++) {
					layers[i].assignImagesForMovie(targetLayer);
					layers[i].setSize(targetLayer.width, targetLayer.height);
				}
				lastTick = tick;
			}
		}
	}
}


/**
 * AffineLayer 対応拡張処理＋ムービー再生機能対応
 */
class GraphicLayerEx extends GraphicLayer {

    function GraphicLayerEx() {
        super.GraphicLayer(...);
    }

	function finalize()
	{
		window.stopAction(this);
		stopRaster();
		clearTile();
		stopMovie();
		stopFire();
		super.finalize(...);
	}

	function _setOptions(elm) {
        if(elm.type !== void)
        {
        	if(/^[0-9]+$/.test(elm.type))
        		type	= +elm.type;
        	else
        		type	= global[elm.type];
        }
		if (elm.reset !== void && elm.reset) {
			reset();
		} else {
			afx = defaultAfx = elm.dafx    if elm.dafx    !== void;
			afy = defaultAfy = elm.dafy    if elm.dafy    !== void;
			afx    = elm.afx    if elm.afx    !== void;
			afy    = elm.afy    if elm.afy    !== void;
			if (elm.time !== void && elm.time > 0) {
				if (elm.left !== void || elm.top !== void) {
					setMove(elm.left !== void ? +elm.left : left,
							elm.top !== void ? +elm.top : top,
							+elm.time, +elm.accel, +elm.nowait);
					// パラメータ反映させないため
					delete elm.left if elm.left !== void;
					delete elm.top  if elm.top  !== void;
				}
				if (elm.rotate !== void) {
					setRotateTime(+elm.rotate, +elm.time, +elm.accel, +elm.nowait);
				}
				if (elm.zoom !== void) {
					setZoomTime(+elm.zoom, +elm.time, +elm.accel, +elm.nowait);
				} else {
					if (elm.zoomx !== void) {
						setZoomXTime(+elm.zoomx, +elm.time, +elm.accel, +elm.nowait);
					}
					if (elm.zoomy !== void) {
						setZoomYTime(+elm.zoomy, +elm.time, +elm.accel, +elm.nowait);
					}
				}
				if (elm.raster !== void) {
					setRasterTime(+elm.raster, elm.rasterlines, elm.rastercycles, +elm.time, +elm.accel, +elm.nowait);
				}
				if (elm.tilex !== void || elm.tiley !== void) {
					setTilePos(elm.tilex !== void ? +elm.tilex : tilex,
							   elm.tiley !== void ? +elm.tiley : tiley,
							   +elm.time, +elm.accel, +elm.nowait);
				}
			} else {
				rotate = +elm.rotate if elm.rotate !== void;
				if (elm.zoom !== void) {
					zoom   = +elm.zoom   if elm.zoom   !== void;
				} else {
					zoomx  = +elm.zoomx  if elm.zoomx  !== void;
					zoomy  = +elm.zoomy  if elm.zoomy  !== void;
				}
				raster = +elm.raster if elm.raster !== void;
				rasterLines = +elm.rasterlines if elm.rasterlines !== void;
				rasterCycle = +elm.rastercycle if elm.rastercycle !== void;
				tilex = +elm.tilex if elm.tilex !== void;
				tiley = +elm.tiley if elm.tiley !== void;
			}
		}
		visvalue = +elm.visvalue if elm.visvalue !== void;
	}
	
	
    function setOptions(elm) {
		var e = %[];
		(Dictionary.assign incontextof e)(elm);
		_setOptions(e);
		if (fire !== void) {
			fire.setOptions(elm);
		}
		super.setOptions(e);
	}

	function loadImages(elm)
	{
		stopRaster();
		clearTile();
		stopMovie();
		stopFire();
		super.loadImages(elm);
		if (elm !== void) {
			if (elm.color === void && elm.storage === void) {
				if (elm.tile !== void) {
					if (elm.width !== void || elm.height !== void) {
						layer.setSize(elm.width !== void ? +elm.width : width,
									  elm.height !== void ? +elm.height : height);
					}
					setTileImage(elm.tile);
				} else if (elm.movie !== void) {
					startMovie(elm.movie, +elm.loop, +elm.alpha);
				} else if (elm.fire !== void) {
					startFire(elm.fire, elm);
				}
			}
			_setOptions(elm);
		}
	}
	
    /**
     * 継承して拡張。reset() させる
     */
	function freeImage() {
		stopRaster();
		clearTile();
		stopMovie();
		stopFire();
		super.freeImage(...);
		reset();
    }
    
    function store()
    {
		var dic = super.store();
        dic.rotate = rotate;
        dic.zoomx  = zoomx;
        dic.zoomy  = zoomy;
        dic.afx    = afx;
		dic.afy    = afy;
		dic.hide   = hide;
		dic.visvalue = visvalue;
		if (tileImage !== void) {
			dic.tileImage = tileImage;
			dic.tilex = tilex;
			dic.tiley = tiley;
			dic.width = width;
			dic.height = height;
		}
		if (movieFile !== void) {
			dic.movieFile = movieFile;
			dic.movieLoop = movieLoop;
			dic.movieAlpha = movieAlpha;
		}
		if (fire !== void) {
			dic.fireInfo = %[];
			(Dictionary.assign incontextof dic.fireInfo)(fire.info);
		}
		if (_rasterStart !== void) {
			dic.raster      = _raster;
			dic.rasterLines = _rasterLines;
			dic.rasterCycle = _rasterCycle;
		}
		return dic;
    }

	function restore(dic)
	{
		stopRaster();
		clearTile();
		stopMovie();
		stopFire();
		super.restore(dic);
		rotate = dic.rotate if dic.rotate !== void; 
		zoomx  = dic.zoomx  if dic.zoomx  !== void;
		zoomy  = dic.zoomy  if dic.zoomy  !== void;
		afx    = dic.afx    if dic.afx    !== void;
		afy    = dic.afy    if dic.afy    !== void;
		hide   = dic.hide   if dic.hide   !== void;
		visvalue = dic.visvalue if dic.visvalue !== void;
		if (dic.tileImage !== void) {
			setSize(dic.width, dic.height);
			try {
				setTileImage(dic.tileImage);
			} catch (e) {
				dm("ロード時にタイル画像の読み込みに失敗しました:" + tileImage);
			}
			setTilePos(dic.tilex, dic.tiley);
		} else if (dic.movieFile !== void) {
			try {
				startMovie(dic.movieFile, dic.movieLoop, dic.movieAlpha);
			} catch (e) {
				dm("ロード時に動画の読み込みに失敗しました:" + movieFile);
			}
		} else if (dic.fireInfo !== void) {
			try {
				startFire(dic.fireInfo.storage, dic.fireInfo);
			} catch (e) {
				dm("ロード時に炎エフェクトの読み込みに失敗しました:" + movieFile);
			}
		}
		if (dic.raster !== void) {
			raster = dic.raster;
			rasterLines = dic.rasterLines;
			rasterCycle = dic.rasterCycle;
		}
	}

    // --------------------------------------------
	// ムービー拡張
    // --------------------------------------------

	// ムービー用情報
	var movie;
	var movieFile;
	var movieLoop;
	var movieAlpha;

	/**
	 * ムービー再生開始
	 */
	function startMovie(filename, loop=false, alpha=false) {
		//dm("ムービー開始 %s %d %d".sprintf(filename, loop, alpha));
		movieFile = filename;
		movieLoop = loop;
		movieAlpha = alpha;
		movie = window.getLayerMovie();
		movie.entry(this);
		movie.start(filename, loop, alpha);
	}

	/**
	 * ムービー終了通知
	 */
	function notifyStopMovie() {
		//dm("ムービー終了通知");
		stopMovie();
	}

	/**
	 * ムービー停止
	 */
	function stopMovie() {
		if (movie !== void) {
			//dm("ムービー停止:" + Scripts.getTraceString());
			movieFile = void;
			movie.leave(this);
			movie = void;
		}
	}

    // --------------------------------------------
	// ムービー拡張
    // --------------------------------------------

	// ムービー用情報
	var fire;

	/**
	 * ムービー再生開始
	 */
	function startFire(storage, elm) {
		//dm("ムービー開始 %s %d %d".sprintf(filename, loop, alpha));
		fire = window.getLayerFire();
		fire.entry(this);
		fire.start(storage, elm);
		setSize(fire.width, fire.height);
	}
	
	/**
	 * ムービー終了通知
	 */
	function notifyStopFire() {
		//dm("ムービー終了通知");
		stopFire();
	}

	/**
	 * ムービー停止
	 */
	function stopFire() {
		if (fire !== void) {
			fire.leave(this);
			fire = void;
		}
	}

	// -------------------------------------------------
	
	function assignImagesForMovie(src) {
		super.assignImages(src);
		applyColorCorrection(this, Anim_loadParams);
	}
	
	function assign(src)
	{
		// assign オーバーライド
		super.assign(src);
		hide = src.hide;
		visvalue = src.visvalue;
		clearTile();
		stopMovie();
		stopFire();
		// ムービー情報をコピー
		if (src.movie !== void) {
			movieFile = src.movieFile;
			movieLoop = src.movieLoop;
			movieAlpha = src.movieAlpha;
			movie = src.movie;
			movie.entry(this);
		}
		// タイル情報をコピー
		if (src.tileImage !== void) {
			setTileImage(src.tileImage);
			setTilePos(src.tilex, src.tiley);
		}
		// 炎エフェクト情報をコピー
		if (src.fire !== void) {
			fire = src.fire;
			fire.entry(this);
		}
		// アクションもコピー
		window.copyAction(src, this, onActionCompleted);
	}

    // --------------------------------------------
    // アクション系拡張
    // --------------------------------------------

    var hide; // アクション終了時に消去

	function onActionCompleted() {
		visible = false if (hide);
    }

	function getActionResult() {
		return window.getActionResult(this);
	}
	
	function beginAction(elm) {
		var nowait;
		if (elm) {
			var target = (elm instanceof "Array")  ? elm[elm.count-1] : elm;
			hide   = target.hide   if target.hide !== void;
			nowait = target.nowait if target.nowait !== void;
		}
		//dm("アクション開始 hide:%d nowait:%d".sprintf(hide, nowait));
		return window.beginAction(this, elm, onActionCompleted, nowait);
    }

    function stopAction() {
        window.stopAction(this);
    }

    function reset() {
		hide = false;
		stopAction();
		super.reset();
    }

	var _visvalue = 0;
	property visible {
		getter() {
			return _visvalue > 0;
		}
		setter(v) {
			_visvalue = v ? 100 : 0;
			opacity = _opacity;
		}
	}

	property visvalue {
		getter() {
			return _visvalue;
		}
		setter(v) {
			_visvalue = v;
			opacity = _opacity;
		}
	}
	
	var _opacity = 255;
	property opacity {
		getter() {
			return _opacity;
		}
		setter(v) {
			_opacity = v === void ? defaultOpacity : v;
			var o = _opacity * _visvalue / 100;
			super.opacity = o;
			super.visible = o > 0;
		}
	}
	
    /**
     * 不透明度を指定する
     * @param opacity 不透明度
     * @time 変更にかかる時間
     * @accel 加減速指定
     */
	function setOpacityTime(o, time, accel=0, nowait=false) {
		if (time) {
			beginAction(%[opacity:%[handler:MoveAction, value:o, time:time, accel:accel, nowait:nowait]]);
		} else {
            opacity = o;
        }
    }

    /**
     * 回転量を指定する
     * @param r 回転量
     * @time 変更にかかる時間
     */
    function setRotateTime(r, time, accel=0, nowait=false) {
		if (time) {
			beginAction(%[rotate:%[handler:MoveAction, value:r, time:time, accel:accel, nowait:nowait]]);
        } else {
            rotate = r;
        }
    }
    
    /**
     * ズーム量を指定する
     * @param zoom 回転量
     * @time 変更にかかる時間
     */
	function setZoomTime(z, time, accel=0, nowait=false) {
        if (time) {
			beginAction(%[zoom:%[handler:MoveAction, value:z, time:time, accel:accel, nowait:nowait]]);
		} else {
			zoom = z;
		}
	}

    /**
	 * Xズーム量を指定する
     * @param zoom 回転量
     * @time 変更にかかる時間
     */
	function setZoomXTime(z, time, accel=0, nowait=false) {
		if (time) {
			beginAction(%[zoomx:%[handler:MoveAction, value:z, time:time, accel:accel, nowait:nowait]]);
		} else {
			zoomx = z;
		}
	}

    /**
	 * Xズーム量を指定する
     * @param zoom 回転量
     * @time 変更にかかる時間
     */
	function setZoomYTime(z, time, accel=0, nowait=false) {
		if (time) {
			beginAction(%[zoomy:%[handler:MoveAction, value:z, time:time, accel:accel, nowait:nowait]]);
		} else {
			zoom = z;
		}
	}
	
    /**
     * ラスタ量を指定する
	 * @param raster 回転量
     * @time 変更にかかる時間
     */
	function setRasterTime(r, lines, cycle, time, accel=0, nowait=false) {
		rasterLines = +lines if lines !== void;
		rasterCycle = +cycle if cycle !== void;
		if (time) {
			beginAction(%[raster:%[handler:MoveAction, value:r, time:time, accel:accel, nowait:nowait]]);
		} else {
			raster = r;
		}
    }
	
    /**
     * 自動移動を指定する
     * @param x X位置
     * @param y Y位置
	 * @param accele 負:減速 0:線形 正:加速
	 * @param nowait 待たない
     * @time 変更にかかる時間
     */
	function setMove(x, y, time, accel=0, nowait=false) {
		if (time) {
			beginAction(%[left:%[handler:MoveAction, value:x, time:time, accel:accel],
					  top:%[handler:MoveAction, value:y, time:time, accel:accel], nowait:nowait]);
        } else {
			stopAction();
			setPos(x, y);
        }
    }

	// -------------------------------------------------------
	// タイル描画処理

	// タイル情報保持用レイヤ
	var tileLayer;

	function clearTile() {
		tileImage = void;
		if (tileLayer !== void) {
			invalidate tileLayer;
			tileLayer = void;
			_doTile = false;
		}
		_tilex = 0;
		_tiley = 0;
	}
	
	// タイル画像
	var tileImage;
	function setTileImage(image) {
		if (tileImage != image) {
			tileImage = image;
			if (tileImage !== void) {
				if (tileLayer === void) {
					tileLayer = new global.Layer(window, this);
				}
				tileLayer.loadImages(tileImage);
				tileLayer.setSizeToImageSize();
				updateTile();
			}
		}
	}

	var _doTile;
	
	// タイル再描画処理
	function updateTile() {
		if (tileLayer !== void) {
			_doTile = true;
			update();
		}
	}

	// 再描画処理
	function onPaint() {
		if (_doTile && tileLayer !== void) {
			tileRect(0, 0, width, height, tileLayer, _tilex, _tiley);
			applyColorCorrection(this, Anim_loadParams);
			_doTile = false;
		}
		super.onPaint(...);
	}
	
	// タイルX位置
	var _tilex;
	property tilex {
		getter() {
			return _tilex;
		}
		setter(v) {
			_tilex = v;
			updateTile();
		}
	}

	// タイルY位置
	var _tiley;
	property tiley {
		getter() {
			return _tiley;
		}
		setter(v) {
			_tiley = v;
			updateTile();
		}
	}

	// タイル位置指定
	function setTilePos(x, y, time=0, accel=0, nowait=false) {
		if (time > 0) {
			beginAction(%[tilex:%[handler:MoveAction, value:x, time:time, accel:accel, nowait:nowait],
					  tiley:%[handler:MoveAction, value:y, time:time, accel:accel, nowait:nowait]]);
		} else {
			_tilex = x;
			_tiley = y;
			updateTile();
		}
	}
}

class CharacterLayer extends GraphicLayerEx
{
	// 前景レイヤ
	var autoHide = false; // メッセージレイヤと一緒に隠れることができるか
	var invisibleByUser = false; // ユーザにより一時的に不可視
    var visibleBeforeUserInvisible  = false;
    var level; // 表示レベル補正用

	function CharacterLayer(win, par, name, id, level)
	{
        super.GraphicLayerEx(win, par, name, id);
		freeImage();
		type = ltCoverRect;
		hitType = htMask;
        hitThreshold = 64;
        this.level = level;
	}

	function finalize()
	{
		super.finalize(...);
	}

	function onHitTest(x, y, hit)
	{
		// onHitTest オーバーライド
		if(!hit || provinceActions === void)
		{
			// hit==false または領域アクションが定義されていない場合
			super.onHitTest(x, y, false);
		}
		else
		{
			// hit==true かつ 領域アクションが定義されている場合
			super.onHitTest(x, y, !window.messageLayerHiding);
		}
		return;
	}

	function assign(src)
	{
		// assign オーバーライド
		super.assign(src);
		assignVisibleState(src);
		// 親クラスの assign は可視・不可視などの情報をコピーしないため
		// ここで assignVisibleState を呼ぶ必要がある
        autoHide = src.autoHide;
        level = src.level;
	}

	function setOptions(elm)
	{
		// setOptions オーバーライド
        super.setOptions(elm);
		autoHide = +elm.autohide if elm.autohide !== void;
	}

	function atEndOfTransition(src, withchildren, exchange)
	{
		// atEndOfTransition オーバーライド
		super.atEndOfTransition(...);
		if(src == null)
		{
			//・メッセージレイヤ、前景レイヤが、srcなしでトランジション (children=true)
			//　何もしなくてよいが、終了後それらの子レイヤは自動的に非表示になる。
		}
		else
		{
			//・メッセージレイヤ、前景レイヤが、srcありでトランジション (children=true)
			//　重要な情報をトランジション元と交換、exchange=false の場合はさらに
			//　トランジション元の情報をトランジション先にコピー。ただし、このコピーの際に
			//　ウィンドウの可視・不可視の情報はコピーしない。
			super.assign(src);
			exchangeInfo();
			window.swapCharacterLayer(id);
		}
	}

	function assignTransSrc()
	{
		// トランジションもと(comp)をコピーする
		assign(comp);
	}

	function setHiddenStateByUser(b)
	{
		// ユーザが右クリックなどでメッセージレイヤを一時的に隠すときに
		// 呼ばれる
		if(autoHide)
		{
			if(b)
			{
				visibleBeforeUserInvisible = visible;
				invisibleByUser = true; // ユーザにより一時的に不可視
				visible = false;
			}
			else
			{
				invisibleByUser = false; // 可視
				visible = visibleBeforeUserInvisible;
			}
		}
	}

	function onMouseDown(x, y, button)
	{
		// マウスが押された
		super.onMouseDown(...);
		if(button == mbLeft)
			processProvince(x, y);
		else if(button == mbRight)
			window.onPrimaryRightClick();
			// 右ボタンは window.onPrimaryRightClick() を呼ぶ
	}

	function store()
	{
		// store オーバーライド
		var dic = super.store();
        dic.autoHide = autoHide;
        dic.level    = level;
        return dic;
	}

	function restore(dic)
	{
		// restore オーバーライド
		autoHide = +dic.autoHide if dic.autoHide !== void;
        level    = +dic.level if dic.level !== void;
        super.restore(dic);
	}
}
