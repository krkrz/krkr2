/**
 * 環境BGMオブジェクト
 */
class KAGEnvBgm extends KAGEnvBase {
	
	/**
	 * コンストラクタ
	 */
	function KAGEnvBgm(env) {
		super.KAGEnvBase(env, "bgm");
	}

	function finalize() {
		super.finalize();
	}

	var filename;
	var loop;
	var paused;
	var volume;
	
	var play;
	var stop;
	var pause;
	var resume;
	var fade;
	var bgmchangeflag;

	// ボリュームが動画あわせになってるフラグ
	var mvolumeflag;

	// BGM停止時コールバック
	function onStop() {
		if (mvolumeflag) {
			var bgm = env.getBGMOBJ();
			bgm.restoreSystemState(kag.scflags);
			mvolumeflag = false;
		}
	}
	
    /**
     * セーブ処理
     */
    function onStore(f) {
		//dm("BGM保存:" + filename);
		f.filename = filename if filename !== void;
		f.loop = loop if loop !== void && loop != true;
		f.paused = paused if paused !== void && paused != false;
		f.volume = volume if volume !== void && volume != 100;
	}

	function onRestore(f) {
		if (f !== void) {
			//dm("BGM復帰:" + f.filename);
			filename = f.filename;
			loop = f.loop !== void ? f.loop : true;
			paused = f.paused !== void ? f.paused : false;
			if (f.filename != "") {
				setPlay(f.filename, %[loop:f.loop]);
				if (f.volume !== void) {
					fade.volume = volume = f.volume;
				}
				if (f.paused) {
					setPause(0);
				}
			}
		}
	}
	
    /**
     * 再生処理
     * @param param 再生対象ファイル
     */
    function setPlay(param, elm) {
		if (param !== void) {
			stop = void;
			pause = void;
			resume = void;
			filename = param;
			loop = elm.loop;
			paused = false;
			play = %[];
			(Dictionary.assign incontextof play)(elm, false);
			if (play.fade === void && play.time === void) {
				fade = %[];
				fade.volume = volume = 100;
			}
			// 再生既読フラグ
			if (elm.noflag === void) {
				env.setSFlag("bgm_" + (param.toUpperCase()));
			}
			doflag = true;
        }
    }

	function _getTimeParam(param, elm) {
		return (elm !== void && elm.time !== void) ? +elm.time : +param;
	}

    /**
     * 停止処理
     * @param param フェードアウト時間
     */
    function setStop(param, elm) {
		play = void;
		fade = void;
		pause = void;
		resume = void;
		filename = void;
		paused = false;
		stop = _getTimeParam(param, elm);
		doflag = true;
    }

    /**
     * ポーズ処理
     * @param param フェードアウト時間
     */
	function setPause(param, elm) {
		if (filename !== void) {
			resume = void;
			pause = _getTimeParam(param, elm);
			paused = true;
			doflag = true;
		}
    }

    /**
	 * 再開
	 */
	function setResume(param, elm) {
		if (filename !== void) {
			pause = void;
			resume = _getTimeParam(param, elm);
			paused = false;
			doflag = true;
		}
    }

    /**
     * 音量フェード
     * @param param フェード時間
     */
	function setFade(param, elm) {
		fade = %[];
		fade.volume = volume = +param;
		fade.time   = elm.time;
    }

    var bgmcommands = %[
	tagname : null, 
	taglist : null, 
	storage : setPlay incontextof this,
    play : setPlay incontextof this,
    stop : setStop incontextof this,
    pause : setPause incontextof this,
	resume : setResume incontextof this,
    fade : setFade incontextof this,
    noxchg : null,
    loop : null,
    time : null,
    start : null,
    paused : null,
    intime : null,
    outtime : null,
    overlap : null,
    canskip : null,
	noflag : null,
    nosync : null,
	sync : null,
	wait : null,
	delayrun : null,
	mvolume : null, // XXX ムービー音量にあわせて再生
        ];

    var doflag;
    
    /**
     * コマンドの実行
     * @param cmd コマンド
     * @param param パラメータ
     * @param elm 他のコマンドも含む全パラメータ
     * @return 実行が行われた場合 true
     */
    function doCommand(cmd, param, elm) {
        var func;
        if ((func = bgmcommands[cmd]) !== void) {
            if (func != null) {
                func(param, elm);
            }
            return true;
        }
        // 再生コマンドとみなす
		setPlay(cmd, elm);
        return true;
    }

	function command(elm) {
		doflag = false;
		foreach(elm, doCommand);
		// 何もしなかった場合、かつ、タグ名が bgm でなければそれを再生する
		if (!doflag && elm.tagname != "bgm") {
			setPlay(elm.tagname, elm);
		}
	}

	function checkSync(elm) {
		syncCommand = true if (elm.wait || elm.sync);
	}

	function update(skipMode) {
		var bgm = env.getBGMOBJ();
		var fading = 0;
		if (fade !== void) {
			if (skipMode == SKIP_NONE && fade.time !== void) {
				bgm.fade(fade);
				fading = 1;
			} else {
				bgm.setVolume(fade.volume * 1000);
			}
			fade = void;
		}
		if (play !== void) {
			if (bgm.playingStorage != filename) {
				var time = +play.time;
				if (play.mvolume) {
					// 動画のボリュームに強制であわせる
					dm("動画ボリュームにあわせる");
					bgm.restoreSystemState(%[bgm:%[globalVolume:kag.movieAudioVolume*1000]]);
					mvolumeflag = true;
				}
				if (play.noxchg) {
					if (skipMode == SKIP_NONE && time > 0)  {
						bgm.fadeIn(%[ storage:filename, loop:play.loop, time:time, start:play.start]);
						fading = 1;
					} else {
						bgm.play(%[ storage:filename, loop:play.loop, paused:play.paused, start:play.start]);
					}
				} else {
					if (skipMode == SKIP_NONE && (time > 0 || +play.intime > 0)) {
						var overlap = play.overlap !== void ? +play.overlap : play.time;
						bgm.exchange(%[ storage:filename, loop:play.loop, time:play.time, intime:play.intime, outtime:play.outtime, overlap:overlap, start:play.start]);
						fading = 1;
					} else {
						bgm.play(%[ storage:filename, loop:play.loop, paused:play.paused, start:play.start]);
					}
				}
			}
			env.onBGMStart();
			play = void;
		}
		if (pause !== void) {
			if (pause > 1) {
				bgm.fadePause(%[ time: +pause ]);
				fading = 1;
			} else {
				bgm.pause();
			}
			pause = void;
		} else if (resume !== void) {
			if (resume > 1) {
				bgm.fadeResume(%[ time: +resume ]);
				fading = 1;
				} else {
					bgm.resume();
				}
			resume = void;
		}
		if (stop !== void) {
			if (skipMode == SKIP_NONE && stop > 0 && bgm.inPlaying) {
				bgm.fadeOut(%[ time:stop ]);
				fading = 2;
			} else {
				bgm.stop();
			}
			stop = void;
		}
		return fading;
	}
	
	function sync(elm, skipMode) {
		var fading = update(skipMode);
		var wait = elm.wait !== void ? +elm.wait : elm.sync !== void ? 0 : void;
		if (wait == 1) {
			wait = 0;
		}
		if (wait !== void) {
			var canskip = elm.canskip !== void ? +elm.canskip : true;
			switch (fading) {
			case 1:
				env.addFastTag("wb", %[canskip:canskip, wait:wait]);
				break;
			default:
				env.addFastTag("wl", %[canskip:canskip, wait:wait]);
				break;
			}
		}
	}
};
