// EditLayer.tjs - ï¿½ï¿½sï¿½ÒWï¿½pï¿½Rï¿½ï¿½ï¿½gï¿½ï¿½ï¿½[ï¿½ï¿½ï¿½ï¿½ï¿½Cï¿½ï¿½
// Copyright (C)2001-2009, W.Dee and contributors  ï¿½ï¿½ÏEï¿½zï¿½zï¿½Íï¿½ï¿½Rï¿½Å‚ï¿½

/*
ï¿½Pï¿½ï¿½sï¿½Ì•ÒWï¿½Rï¿½ï¿½ï¿½gï¿½ï¿½ï¿½[ï¿½ï¿½ï¿½Æ‚ï¿½ï¿½Ä“ï¿½ï¿½ì‚·ï¿½éƒŒï¿½Cï¿½ï¿½ï¿½Å‚ï¿½ï¿½B

ï¿½ï¿½ï¿½ï¿½  new EditLayer(<window>, <parent>)
*/

class EditLayer extends KAGLayer
{
	var Edit_text = ""; // ï¿½ÒWï¿½ï¿½ï¿½Ìƒeï¿½Lï¿½Xï¿½g
	var Edit_selStart = 0; // ï¿½Jï¿½[ï¿½\ï¿½ï¿½ï¿½Ê’u
	var Edit_selAnchor = 0; // ï¿½ÍˆÍ‘Iï¿½ï¿½ï¿½Aï¿½ï¿½ï¿½Jï¿½[ï¿½Ê’u
	var Edit_scrollPos = 0; // ï¿½\ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½Ä‚ï¿½ï¿½é¶ï¿½[ï¿½Ì•ï¿½ï¿½ï¿½ï¿½Ê’u
	var Edit_caretLayer; // ï¿½Lï¿½ï¿½ï¿½ï¿½ï¿½bï¿½gï¿½pï¿½ï¿½ï¿½Cï¿½ï¿½
	var Edit_color = clWindow; // ï¿½wï¿½iï¿½F
	var Edit_antialiased = true; // ï¿½Aï¿½ï¿½ï¿½`ï¿½Gï¿½Cï¿½ï¿½ï¿½Aï¿½Xï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½é‚©
	var Edit_opacity = 192; // ï¿½sï¿½ï¿½ï¿½ï¿½ï¿½x
	var Edit_maxChars = 0; // ï¿½Å‘å•¶ï¿½ï¿½ï¿½ï¿½ ( 0 = ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ )
	var Edit_textColor = clWindowText;
	var Edit_blinkTimer;
	var Edit_vertical = false;
	var Edit_mouseDown = false;

	var Edit_selLength = 0; // ï¿½pï¿½~ï¿½ï¿½ï¿½Ü‚ï¿½ï¿½ï¿½ï¿½B
							// ï¿½gï¿½ï¿½È‚ï¿½ï¿½Å‚ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½B

	function EditLayer(win, par, vert = false)
	{
		super.KAGLayer(win, par);

		Edit_vertical = vert;

		hitType = htMask;
		hitThreshold = 0; // ï¿½Sï¿½ï¿½Åƒ}ï¿½Eï¿½Xï¿½Cï¿½xï¿½ï¿½ï¿½gï¿½Í•sï¿½ï¿½ï¿½ï¿½
		focusable = true; // ï¿½tï¿½Hï¿½[ï¿½Jï¿½Xï¿½Íó‚¯ï¿½ï¿½
		useAttention = true; // ï¿½ï¿½ï¿½ï¿½ï¿½|ï¿½Cï¿½ï¿½ï¿½gï¿½ï¿½ï¿½gï¿½pï¿½ï¿½ï¿½ï¿½

		Edit_caretLayer = new global.Layer(win, this);
		Edit_caretLayer.hitType = htMask;
		Edit_caretLayer.hitThreshold = 256; // ï¿½ï¿½ï¿½Sï¿½Éƒ}ï¿½Eï¿½Xï¿½Cï¿½xï¿½ï¿½ï¿½gï¿½Í“ï¿½ï¿½ï¿½
		resizeCaret();

		imeMode = imDontCare; // IME ï¿½gï¿½pï¿½ï¿½

		// ï¿½_ï¿½Å—pï¿½^ï¿½Cï¿½}ï¿½ï¿½pï¿½ï¿½
		Edit_blinkTimer = new Timer(onBlink, '');

		cursor = vert ? crHBeam : crIBeam;

		update();
	}

	function finalize()
	{
		invalidate Edit_caretLayer;
		invalidate Edit_blinkTimer;
		super.finalize(...);
	}

	function assign(src)
	{
		assignImages(src, true);
		Edit_text = src.Edit_text;
		Edit_selStart = src.Edit_selStart;
		Edit_selAnchor = src.Edit_selAnchor;
		Edit_scrollPos = src.Edit_scrollPos;
		Edit_color = src.Edit_color;
		Edit_textColor = src.Edit_textColor;
		Edit_antialiased = src.Edit_antialiased;
		Edit_opacity = src.Edit_opacity;
		Edit_vertical = src.Edit_vertical;
		Edit_maxChars = src.Edit_maxChars;
		var f = font;
		var sf = src.font;
		f.face = sf.face;
		f.angle = sf.angle;
		f.bold = sf.bold;
		f.italic = sf.italic;
		f.height = sf.height;
		cursor = src.cursor;
		resizeCaret();
		update();
	}

	function resizeCaret()
	{
		// ï¿½Lï¿½ï¿½ï¿½ï¿½ï¿½bï¿½gï¿½ÌƒTï¿½Cï¿½Yï¿½Ìï¿½ï¿½ï¿½
		if(Edit_vertical)
		{
			var h = 2; // 2 ï¿½Å’ï¿½
			var w = font.height;
			w = -w if(w<0);
			Edit_caretLayer.setImageSize(w, h);
			Edit_caretLayer.setSizeToImageSize();
			Edit_caretLayer.colorRect(0, 0, w, h, Edit_textColor);
		}
		else
		{
			var w = 2; // 2 ï¿½Å’ï¿½
			var h = font.height;
			h = -h if(h<0);
			Edit_caretLayer.setImageSize(w, h);
			Edit_caretLayer.setSizeToImageSize();
			Edit_caretLayer.colorRect(0, 0, w, h, Edit_textColor);
		}
	}

	function getCaretXPos()
	{
		// ï¿½Lï¿½ï¿½ï¿½ï¿½ï¿½bï¿½gï¿½ÌˆÊ’uï¿½ï¿½ï¿½æ“¾
		var bch = Edit_text.substring(0, Edit_selStart); // ï¿½Lï¿½ï¿½ï¿½ï¿½ï¿½bï¿½gï¿½È‘Oï¿½Ì•ï¿½ï¿½ï¿½ï¿½ï¿½
		var bchw = font.getTextWidth(bch);
		var sch = Edit_text.substring(0, Edit_scrollPos); // ï¿½\ï¿½ï¿½ï¿½È‘Oï¿½Ì•ï¿½ï¿½ï¿½
		var schw = font.getTextWidth(sch);
		return bchw - schw + 3;
	}

	function ensureCaretVisible(step = 1)
	{
		// ï¿½Lï¿½ï¿½ï¿½ï¿½ï¿½bï¿½gï¿½ï¿½ï¿½mï¿½ï¿½ï¿½É•\ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½Ê’uï¿½Ü‚ÅƒXï¿½Nï¿½ï¿½ï¿½[ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½
		var sp_save = Edit_scrollPos;
		var txtlen = Edit_text.length;
		if(Edit_selStart == txtlen)
			while(Edit_scrollPos >= 0 && getCaretXPos() <= 3) Edit_scrollPos-=step;
		else
			while(Edit_scrollPos >= 0 && getCaretXPos() < 3) Edit_scrollPos-=step;
		var lim = Edit_vertical ? imageHeight : imageWidth;
		while(Edit_scrollPos < txtlen && getCaretXPos() >= lim -3) Edit_scrollPos+=step;
		if(Edit_scrollPos < 0) Edit_scrollPos = 0;
		if(Edit_scrollPos > txtlen) Edit_scrollPos = txtlen;
		if(sp_save != Edit_scrollPos) update();
	}

	function setCaretLayerPos()
	{
		resizeCaret();
		var xpos = getCaretXPos() -1;
		if(Edit_vertical)
		{
			Edit_caretLayer.top = xpos;
			Edit_caretLayer.left = imageWidth - 3 - Edit_caretLayer.imageWidth;
			setAttentionPos(imageWidth - 3 , Edit_caretLayer.top);
		}
		else
		{
			Edit_caretLayer.top = 3;
			Edit_caretLayer.left = xpos;
			setAttentionPos(xpos, 3);
		}
	}

	function showCaret()
	{
		setCaretLayerPos();
		Edit_caretLayer.visible = true;
		Edit_blinkTimer.interval = 500;
		Edit_blinkTimer.enabled = true;
	}

	function hideCaret()
	{
		Edit_blinkTimer.enabled = false;
		Edit_caretLayer.visible = false;
	}

	function onBlink()
	{
		Edit_caretLayer.visible = ! Edit_caretLayer.visible;
	}

	function stepLeft(keepAnchor = false)
	{
		var oldAnchor = Edit_selAnchor;

		// ï¿½Lï¿½ï¿½ï¿½ï¿½ï¿½bï¿½gï¿½ï¿½ï¿½ï¿½ï¿½ï¿½
		if(Edit_selStart > 0) Edit_selStart --, ensureCaretVisible(3);
		if(!keepAnchor) Edit_selAnchor = Edit_selStart;
		if(keepAnchor || oldAnchor != Edit_selAnchor)
			update();
		showCaret();
	}

	function stepRight(keepAnchor = false)
	{
		var oldAnchor = Edit_selAnchor;

		// ï¿½Lï¿½ï¿½ï¿½ï¿½ï¿½bï¿½gï¿½ï¿½ï¿½Eï¿½ï¿½
		if(Edit_selStart < Edit_text.length) Edit_selStart ++, ensureCaretVisible(3);
		if(!keepAnchor) Edit_selAnchor = Edit_selStart;
		if(keepAnchor || oldAnchor != Edit_selAnchor)
			update();
		showCaret();
	}

	function getCaretPosFromX(x)
	{
		// ï¿½Nï¿½ï¿½ï¿½bï¿½Nï¿½ï¿½ï¿½ê‚½ï¿½Ê’uï¿½ï¿½ï¿½ï¿½ï¿½ï¿½oï¿½ï¿½
		x -= 3;
		var text = Edit_text.substring(Edit_scrollPos);
		var i = 0;
		for(;;)
		{
			var tx = text.substring(0, i);
			var cw;
			if((cw = font.getTextWidth(tx)) > x)
			{
				i --;
				var cc = text.substring(i, 1);
				var ccw = font.getTextWidth(cc);
				cw -= ccw;
				if(x > cw + (ccw>>1)) i++;
				if(i < 0) i = 0;
				if(i > text.length) i = text.length;
				return i + Edit_scrollPos;
			}
			i++;
			if(i > text.length) return text.length + Edit_scrollPos;
		}
	}

	function onMouseDown(x, y, button, shift)
	{
		Edit_mouseDown = true;

		// ï¿½Nï¿½ï¿½ï¿½bï¿½Nï¿½ï¿½ï¿½ê‚½
		if(x >= 3 && y >= 3 && x < imageWidth -3 && y < imageHeight - 3)
		{
			var oldStart = Edit_selStart;
			var oldAnchor = Edit_selAnchor;

			if(Edit_vertical)
				Edit_selStart = getCaretPosFromX(y);
			else
				Edit_selStart = getCaretPosFromX(x);

			// Shift ï¿½Lï¿½[ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½Ä‚ï¿½ï¿½È‚ï¿½ï¿½Ì‚Å‚ï¿½ï¿½ï¿½ÎƒAï¿½ï¿½ï¿½Jï¿½[ï¿½ï¿½ï¿½Xï¿½Vï¿½ï¿½ï¿½ï¿½
			if((shift & ssShift) != ssShift)
				Edit_selAnchor = Edit_selStart;

			// ï¿½Xï¿½Vï¿½Ì•Kï¿½vï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ÎXï¿½Vï¿½ï¿½ï¿½ï¿½
			if(oldStart != Edit_selStart || oldAnchor != Edit_selAnchor)
				update();

			ensureCaretVisible();
			showCaret();
		}

		focus();
		super.onMouseDown(...);
	}

	function onMouseUp()
	{
		Edit_mouseDown = false;

		super.onMouseUp(...);
	}

	function onMouseMove(x, y, button)
	{
		if(Edit_mouseDown)
		{
			// ï¿½ï¿½ï¿½ï¿½ï¿½Å‚ÌˆÚ“ï¿½ï¿½ÍƒAï¿½ï¿½ï¿½Jï¿½[ï¿½Íï¿½Î‚ÉˆÚ“ï¿½ï¿½ï¿½ï¿½È‚ï¿½

			if(Edit_vertical)
			{
				if(y >= 3 && y < (imageHeight - 3))
				{
					var oldStart = Edit_selStart;

					Edit_selStart = getCaretPosFromX(y);

					// ï¿½Xï¿½Vï¿½Ì•Kï¿½vï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ÎXï¿½Vï¿½ï¿½ï¿½ï¿½
					if(oldStart != Edit_selStart)
						update();

					ensureCaretVisible();
					showCaret();
				}
				else
				{
					if(y <= 3)
						stepLeft(true);
					else
					if(y > (imageHeight - 3))
						stepRight(true);
				}
			}
			else
			{
				if(x >= 3 && x < (imageWidth - 3))
				{
					var oldStart = Edit_selStart;

					Edit_selStart = getCaretPosFromX(x);

					// ï¿½Xï¿½Vï¿½Ì•Kï¿½vï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ÎXï¿½Vï¿½ï¿½ï¿½ï¿½
					if(oldStart != Edit_selStart)
						update();

					ensureCaretVisible();
					showCaret();
				}
				else
				{
					if(x <= 3)
						stepLeft(true);
					else
					if(x > (imageWidth - 3))
						stepRight(true);
				}
			}
		}

		super.onMouseMove(...);
	}

	/*
	 ï¿½Iï¿½ï¿½ÍˆÍ‚ÉŠÜ‚Ü‚ï¿½é•¶ï¿½ï¿½ï¿½ï¿½Sï¿½Äíœï¿½ï¿½ï¿½ï¿½
	 ï¿½íœï¿½ï¿½ï¿½ï¿½ï¿½Ì‚Å‚ï¿½ï¿½ï¿½ï¿½ true ï¿½ï¿½ï¿½Aï¿½ï¿½ï¿½ï¿½ï¿½Å‚È‚ï¿½ï¿½ï¿½ï¿½ false ï¿½ï¿½Ô‚ï¿½
	*/
	function deleteSelectRange()
	{
		if(Edit_selStart == Edit_selAnchor)
			return false;

		var sp, ep;

		if(Edit_selStart > Edit_selAnchor)
		{
			sp = Edit_selAnchor;
			ep = Edit_selStart;
			Edit_selStart = Edit_selAnchor;
		}
		else
		{
			sp = Edit_selStart;
			ep = Edit_selAnchor;
			Edit_selAnchor = Edit_selStart;
		}

		Edit_text = Edit_text.substring(0, sp) + Edit_text.substring(ep);

		ensureCaretVisible();
		showCaret();
		update();

		return true;
	}

	/*
	 pos ï¿½ÌˆÊ’uï¿½É‚ï¿½ï¿½é•¶ï¿½ï¿½ï¿½ï¿½ï¿½Iï¿½ï¿½ÍˆÍ‚ÉŠÜ‚Ü‚ï¿½Ä‚ï¿½ï¿½é‚©ï¿½Ç‚ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½×‚ï¿½
	*/
	function isSelected(pos)
	{
		if(Edit_selStart == Edit_selAnchor)
			return false;

		if(Edit_selStart > Edit_selAnchor)
			return ((Edit_selAnchor <= pos && Edit_selStart > pos) ? true : false);
		else
			return ((Edit_selAnchor > pos && Edit_selStart <= pos) ? true : false);

		return true;/* dead code */
	}

	/* ï¿½Sï¿½Iï¿½ğ‘€ï¿½ */
	function operationAllSelection()
	{
		Edit_selAnchor = 0;
		Edit_selStart = Edit_text.length;
		ensureCaretVisible();
		showCaret();
		update();
	}

	/* ï¿½Rï¿½sï¿½[ï¿½ï¿½ï¿½ï¿½ */
	function operationCopy()
	{
		if(Edit_selAnchor == Edit_selStart)
			return;

		var start, len;
		var text = "";

		if(Edit_selStart > Edit_selAnchor)
		{
			start = Edit_selAnchor;
			len = Edit_selStart - start;
		}
		else
		{
			start = Edit_selStart;
			len = Edit_selAnchor - start;
		}

		Clipboard.asText = Edit_text.substring(start, len);
	}

	/* ï¿½yï¿½[ï¿½Xï¿½gï¿½ï¿½ï¿½ï¿½ */
	function operationPaste()
	{
		// ï¿½eï¿½Lï¿½Xï¿½gï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½Î‚ï¿½ï¿½ï¿½ï¿½ï¿½ñ‰½‚ï¿½ï¿½ï¿½ï¿½È‚ï¿½
		if(Clipboard.hasFormat(cbfText))
		{
			var text = Clipboard.asText;

			// ï¿½ï¿½ï¿½ï¿½È‚É‚ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½Ì“ï¿½ï¿½È‚ï¿½ï¿½æ‚£
			if(Edit_maxChars)
			{
				var acceptLen = Edit_maxChars - Edit_text.length;
				text = text.substring(0, acceptLen);

				// ï¿½Ş‚ï¿½ï¿½
				if(text == "")
					return;
			}

			insertCharacter(text);
		}
	}

	/* ï¿½Jï¿½bï¿½gï¿½ï¿½ï¿½ï¿½ */
	function operationCut()
	{
		operationCopy();
		deleteSelectRange();
	}

	function insertCharacter(ch)
	{
		// ï¿½Iï¿½ï¿½ÍˆÍ‚Ìƒeï¿½Lï¿½Xï¿½gï¿½ï¿½ï¿½íœï¿½ï¿½ï¿½ï¿½
		deleteSelectRange();

		// ch ï¿½ï¿½ï¿½Jï¿½[ï¿½\ï¿½ï¿½ï¿½Ê’uï¿½É‘}ï¿½ï¿½
		var bcr = Edit_text.substring(0, Edit_selStart);
		var acr = Edit_text.substring(Edit_selStart);
		Edit_text = bcr + ch + acr;
		Edit_selStart += ch.length;
		Edit_selAnchor = Edit_selStart;
		ensureCaretVisible();
		showCaret();
		update();
	}

	function deleteBeforeCaret()
	{
		// ï¿½Lï¿½ï¿½ï¿½ï¿½ï¿½bï¿½gï¿½Ìï¿½Oï¿½Ì•ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ê•¶ï¿½ï¿½ï¿½íœï¿½ï¿½ï¿½ï¿½
		if(!deleteSelectRange() && Edit_selStart > 0)
		{
			var bcr = Edit_text.substring(0, Edit_selStart-1);
			var acr = Edit_text.substring(Edit_selStart);
			Edit_text = bcr + acr;
			Edit_selStart --;
			Edit_selAnchor = Edit_selStart;
			ensureCaretVisible(5);
			showCaret();
			update();
		}
	}

	function deleteAfterCaret()
	{
		// ï¿½Lï¿½ï¿½ï¿½ï¿½ï¿½bï¿½gï¿½Ìï¿½ï¿½Ìˆê•¶ï¿½ï¿½ï¿½ï¿½ï¿½íœï¿½ï¿½ï¿½ï¿½
		if(!deleteSelectRange() && Edit_selStart < Edit_text.length)
		{
			var bcr = Edit_text.substring(0, Edit_selStart);
			var acr = Edit_text.substring(Edit_selStart + 1);
			Edit_text = bcr + acr;
			ensureCaretVisible();
			showCaret();
			update();
		}
	}

	function onKeyPress(key)
	{
		// ï¿½Lï¿½[ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ê‚½
		if(#key >= 32)
		{
			// ï¿½ï¿½ï¿½Ê‚Ì•ï¿½ï¿½ï¿½
			if(!Edit_maxChars || Edit_text.length < Edit_maxChars)
				insertCharacter(key);
			// ï¿½}ï¿½Eï¿½Xï¿½Jï¿½[ï¿½\ï¿½ï¿½ï¿½ï¿½ï¿½êï¿½Iï¿½É”ï¿½\ï¿½ï¿½ï¿½É‚ï¿½ï¿½ï¿½
			window.hideMouseCursor();
		}
		else super.onKeyPress(...);
	}

	function onKeyDown(key, shift)
	{
		// ï¿½ï¿½ï¿½ï¿½ÈƒIï¿½yï¿½ï¿½ï¿½[ï¿½Vï¿½ï¿½ï¿½ï¿½
		if(shift & ssCtrl)
		{
			switch(key)
			{
			case VK_A:	operationAllSelection(); return;
			case VK_C:	operationCopy(); return;
			case VK_X:	operationCut(); return;
			case VK_V:	operationPaste(); return;
			}
		}

		if(key == (Edit_vertical?VK_UP:VK_LEFT)) stepLeft((shift & ssShift) == ssShift);
		else if(key == (Edit_vertical?VK_DOWN:VK_RIGHT)) stepRight((shift & ssShift) == ssShift);
		else if(key == VK_DELETE) deleteAfterCaret();
		else if(key == VK_BACK) deleteBeforeCaret();
		else super.onKeyDown(...);
	}


	function onFocus()
	{
		// ï¿½tï¿½Hï¿½[ï¿½Jï¿½Xï¿½ğ“¾‚ï¿½
		super.onFocus(...);
		showCaret();
	}

	function onBlur()
	{
		// ï¿½tï¿½Hï¿½[ï¿½Jï¿½Xï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½
		super.onBlur(...);
		hideCaret();
	}


	function onPaint()
	{
		// ï¿½`ï¿½ï¿½Ì’ï¿½ï¿½Oï¿½ÉŒÄ‚Î‚ï¿½ï¿½
		// ï¿½ï¿½ï¿½eï¿½ï¿½`ï¿½æ‚·ï¿½ï¿½
		// ï¿½Æ‚è‚ ï¿½ï¿½ï¿½ï¿½ï¿½Nï¿½ï¿½ï¿½A
		var vert = Edit_vertical;
		var imwidth = vert?imageHeight:imageWidth;
		fillRect(0, 0, imageWidth, imageHeight, 0);
		colorRect(0, 0, imageWidth, imageHeight, Edit_color, Edit_opacity);
		// ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½`ï¿½ï¿½
		// ï¿½Oï¿½ï¿½ï¿½[ï¿½oï¿½ï¿½ï¿½Ïï¿½ï¿½ï¿½Aï¿½Iï¿½uï¿½Wï¿½Fï¿½Nï¿½gï¿½ï¿½ï¿½Ïï¿½ï¿½ÍƒAï¿½Nï¿½Zï¿½Xï¿½ï¿½ï¿½xï¿½ï¿½ï¿½Ì‚ÅA
		// ï¿½æ‚­ï¿½gï¿½ï¿½ï¿½Ïï¿½ï¿½Íƒï¿½ï¿½[ï¿½Jï¿½ï¿½ï¿½Éï¿½ï¿½ï¿½ï¿½Ä‚ï¿½ï¿½ï¿½
		var h = font.height;
		h = -h if(h<0);
		var x = vert ? imageWidth - 3 : 3;
		var y = 3;
		var chpos = Edit_scrollPos;
		var text = Edit_text;
		var textlen = text.length;
		var selstart = Edit_selStart;
		var highlightbg = 0xff000000 | clHighlight;
		var highlighttext = clHighlightText;
		var anti = Edit_antialiased;
		var textcolor = Edit_textColor;
		var tx = "";
		if(vert)
		{
			for(;;)
			{
				y = 3 + font.getTextWidth(tx);
				if(y > imwidth) break;
				if(chpos >= textlen) break;
				var ch = text[chpos];
				var chw = font.getTextWidth(ch);
				if(isSelected(chpos++))
				{
					// ï¿½Iï¿½ï¿½Ìˆï¿½ï¿½
					// ï¿½wï¿½iï¿½ï¿½hï¿½ï¿½
					fillRect(x, y, h, chw, highlightbg);
					// ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½
					drawText(x, y, ch, highlighttext, 255, anti);
				}
				else
				{
					// ï¿½Iï¿½ï¿½Ìˆï¿½O
					drawText(x, y, ch, textcolor, 255, anti);
				}
				tx += ch;
			}
		}
		else
		{
			for(;;)
			{
				x = 3 + font.getTextWidth(tx);
				if(x > imwidth) break;
				if(chpos >= textlen) break;
				var ch = text[chpos];
				var chw = font.getTextWidth(ch);
				if(isSelected(chpos++))
				{
					// ï¿½wï¿½iï¿½ï¿½hï¿½ï¿½
					fillRect(x, y, chw, h, highlightbg);
					// ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½
					drawText(x, y, ch, highlighttext, 255, anti);
				}
				else
				{
					// ï¿½Iï¿½ï¿½Ìˆï¿½O
					drawText(x, y, ch, textcolor, 255, anti);
				}
				tx += ch;
			}
		}
		// ï¿½ï¿½ï¿½ï¿½ï¿½Ìˆï¿½Oï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½ï¿½xï¿½Nï¿½ï¿½ï¿½A
		if(vert)
		{
			fillRect(0, imageHeight - 3, imageWidth, 3, 0);
			colorRect(0, imageHeight - 3, imageWidth, 3, Edit_color, Edit_opacity);
		}
		else
		{
			fillRect(imageWidth - 3, 0, 3, imageHeight, 0);
			colorRect(imageWidth - 3, 0, 3, imageHeight, Edit_color, Edit_opacity);
		}
		// ï¿½gï¿½ï¿½`ï¿½ï¿½
		colorRect(0, 0, imageWidth, 1, 0x000000, 128);
		colorRect(0, 1, 1, imageHeight-2, 0x000000, 128);
		colorRect(imageWidth-1, 1, 1, imageHeight-1, 0xffffff, 128);
		colorRect(1, imageHeight-1, imageWidth-2, 1, 0xffffff, 128);
	}

	property text
	{
		setter(x)
		{
			x = "" if x === void;
			Edit_text = string x;
			Edit_selStart = 0;
			Edit_selAnchor = 0;
			Edit_scrollPos = 0;
			setCaretLayerPos();
			update();
		}
		getter
		{
			return Edit_text;
		}
	}

	property maxChars
	{
		setter(x)
		{
			Edit_maxChars = int x;
			Edit_selStart = 0;
			Edit_selAnchor = 0;
			Edit_scrollPos = 0;
			if(Edit_maxChars && Edit_text.length >= Edit_maxChars)
				Edit_text = Edit_text.substring(0, Edit_maxChars);
			setCaretLayerPos();
			update();
		}
		getter
		{
			return Edit_maxChars;
		}
	}

	property selectLength
	{
		getter
		{
			if(Edit_selStart > Edit_selAnchor)
				return Edit_selStart - Edit_selAnchor;
			return Edit_selAnchor - Edit_selStart;
		}
	}

	property width
	{
		setter(x)
		{
			super.width = x;
			imageWidth = x;
			update();
		}
		getter
		{
			return super.width;
		}
	}

	property height
	{
		setter(x)
		{
			super.height = x;
			imageHeight = x;
			update();
		}
		getter
		{
			return super.height;
		}
	}

	function setSize(w, h)
	{
		super.setSize(w, h);
		setImageSize(w, h);
		update();
	}

	property vertical
	{
		setter(x)
		{
			Edit_vertical = int x;
			update();
		}
		getter
		{
			return Edit_vertical;
		}
	}

	property color
	{
		setter(x)
		{
			Edit_color = int x;
			update();
		}
		getter
		{
			return Edit_color;
		}
	}

	property textColor
	{
		setter(x)
		{
			Edit_textColor = int x;
			update();
		}
		getter
		{
			return Edit_textColor;
		}
	}

	property antialiased
	{
		setter(x)
		{
			Edit_antialiased = int x;
			update();
		}
		getter
		{
			return Edit_antialiased;
		}
	}

	property bgOpacity
	{
		setter(x)
		{
			Edit_opacity = int x;
			update();
		}
		getter
		{
			return Edit_opacity;
		}
	}
}


